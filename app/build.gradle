apply plugin: 'com.android.application'
apply plugin: 'bugly'

bugly {
    appId = 'ef48d6a01a'
    appKey = 'd4e77414-fa29-4beb-a003-1398230befbc'
    execute = 'true'
    upload = 'true'
//    outputDir = ''
}

// to speed up builds, add `-PdevBuild` to Command-line Options.
def buildDateTime = project.hasProperty('devBuild') ? 100 : new Date().format('yyyyMMddHH').toInteger()

android {
    sourceSets.main {
        jniLibs.srcDir 'libs'
        jni.srcDirs = [] //disable automatic ndk-build call
    }

    defaultConfig {
        applicationId "cn.rongcloud.rtc"
        minSdkVersion 19
        targetSdkVersion 27
        compileSdkVersion 27
        versionCode buildDateTime
        versionName "4.11.0"

        testApplicationId "cn.rongcloud.rtc.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        // Enabling multidex support.
        multiDexEnabled true

        externalNativeBuild {
            cmake {
                abiFilters 'armeabi-v7a', 'x86'
            }
        }

        // bugly config
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'//, 'armeab', 'x86_64'
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
            zipAlignEnabled false
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false
            jniDebuggable false
            minifyEnabled true
            shrinkResources false
            zipAlignEnabled true
            proguardFile 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    packagingOptions {
        exclude 'lib/armeabi/*.so'
        exclude 'lib/arm64-v8a/*.so'
        exclude 'lib/mips64/*.so'
        exclude 'lib/x86_64/*.so'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:support-annotations:26.0.0"
    implementation "com.android.support:appcompat-v7:26.0.0"
    implementation "com.android.support:design:26.0.0"
    implementation "com.android.support:support-v4:26.0.0"
    implementation("com.github.bumptech.glide:glide:4.9.0") {
        exclude group: "com.android.support"
    }
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.google.code.gson:gson:2.8.1'
    //bugly
    api 'com.tencent.bugly:crashreport:3.2.33'
    //其中latest.release指代最新Bugly SDK版本号，也可以指定明确的版本号，例如2.1.9
    api 'com.tencent.bugly:nativecrashreport:3.7.471'
    //其中latest.release指代最新Bugly NDK版本号，也可以指定明确的版本号，例如3.0
    implementation 'me.yokeyword:indexablerecyclerview:1.3.0'
    implementation 'com.github.duty-os:white-sdk-android:2.7.7'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'junit:junit:4.12'
    implementation 'de.hdodenhof:circleimageview:3.0.0'

    api 'cn.rongcloud.sdk:im_lib:4.0.3.2'
    api 'cn.rongcloud.sdk:rtc_lib:4.0.3.3'
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '26.0.0'
            }
        }
    }
}

task outputPackage() {
    doLast {
        mkdir "output/apk"
        exec { commandLine 'bash', '-c', 'cp -r build/outputs/apk/* output/apk' }
    }
}